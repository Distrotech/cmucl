#!/bin/csh -f
#
#  mk-lisp -- script for building full lisp cores.
#
# $Header: /Volumes/share2/src/cmucl/cvs2git/cvsroot/src/tools/Attic/mk-lisp,v 1.8 1991/11/07 22:58:41 wlott Exp $

if ($#argv) then
	set subdir = $argv[1]
	set features = ($argv[2-])
else
	set subdir = alpha
	set features = ()
endif

set lisp = /afs/cs/project/clisp/build/@sys/$subdir

if (-e $lisp/ldb/kernel.core) then
	set core = $lisp/ldb/kernel.core
else
	if (-e /usr/tmp/kernel.core) then
		set core = /usr/tmp/kernel.core
	else
		echo Can\'t find the kernel.core
	endif
endif

if (-e $lisp/VERSION) then
	set version = `cat $lisp/VERSION`
else
	set version = `/bin/date | awk '{print $3 "-" $2 "-" $6}'`
endif

if (-e $lisp/FEATURES) then
	set features = ($features `cat $lisp/FEATURES`)
endif

# set a bunch of random env vars to fill up space.
setenv FOO XXXXXXXXXXXXXXXX
setenv FOO $FOO$FOO$FOO$FOO
setenv FOO $FOO$FOO$FOO$FOO
setenv BAR $FOO
setenv BAZ $FOO
setenv ACK $FOO
setenv RAZ $FOO
setenv GORP $FOO
setenv BAR2 $FOO
setenv BAZ2 $FOO
setenv ACK2 $FOO
setenv RAZ2 $FOO
setenv GORP2 $FOO

echo Building lisp.core dated $version from the \`\`$subdir\'\' subdir.

$lisp/ldb/ldb -core $core << EOF
(setf *features* (list* $features *features*))
(setf (search-list "lisp:") '("$lisp/"))
(in-package "USER")
(load (open "lisp:tools/worldload.lisp"))
$version
(quit)
EOF

echo 
